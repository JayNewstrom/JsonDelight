ext {
    kotlinVersion = '1.3.11'
    compositeVersion = '0.5.2'

    dep = [
            androidPlugin: 'com.android.tools.build:gradle:3.2.1',
            kotlinPoet: 'com.squareup:kotlinpoet:1.0.0',
            kotlinStdLib: "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
            kotlinReflect: "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion",
            kotlinGradlePlugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion",
            retrofit: 'com.squareup.retrofit2:retrofit:2.5.0',
            jackson: 'com.fasterxml.jackson.core:jackson-core:2.9.8',
            jacksonDatabind: 'com.fasterxml.jackson.core:jackson-databind:2.9.8',
            jsr305: 'com.google.code.findbugs:jsr305:3.0.2',
            compositeRuntime: "com.jaynewstrom.composite:runtime:$compositeVersion",
    ]
}

subprojects {
    group ARTIFACT_GROUP
    version ARTIFACT_VERSION

    buildscript {
        repositories {
            jcenter()
            google()
        }
        dependencies {
            classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
            classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
            classpath dep.kotlinGradlePlugin
        }
    }
    repositories {
        jcenter()
        google()
    }
    configureKotlinLint(it)
}

def configureKotlinLint(Project project) {
    def closure = {
        project.configurations {
            ktlint
        }

        project.dependencies {
            ktlint 'com.github.shyiko:ktlint:0.29.0'
        }

        def ktlintCheck = project.tasks.create('ktlintCheck', JavaExec.class) {
            main = 'com.github.shyiko.ktlint.Main'
            classpath = project.configurations.ktlint
            args 'src/**/*.kt'
        }

        project.check.dependsOn ktlintCheck

        project.tasks.create('ktlintFormat', JavaExec.class) {
            main = 'com.github.shyiko.ktlint.Main'
            classpath = project.configurations.ktlint
            args '-F', 'src/**/*.kt'
        }
    }
    project.plugins.withId('kotlin-android', closure)
    project.plugins.withId('kotlin', closure)
}
