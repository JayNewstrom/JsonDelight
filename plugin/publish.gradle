apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

afterEvaluate { project ->
    if (project.getPlugins().hasPlugin('com.android.library')) {
        task sourcesJar(type: Jar) {
            from android.sourceSets.main.java.srcDirs
            classifier = 'sources'
        }
    } else {
        task sourcesJar(type: Jar, dependsOn: classes) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }
    }

    artifacts {
        archives sourcesJar
    }

    bintray {
        user = project.hasProperty('BINTRAY_USER') ? BINTRAY_USER : ""
        key = project.hasProperty('BINTRAY_API_KEY') ? BINTRAY_API_KEY : ""
        configurations = ['archives']
        publish = true
        pkg {
            repo = ARTIFACT_REPO
            name = ARTIFACT_NAME
            userOrg = user
            licenses = ['Apache-2.0']
            vcsUrl = ARTIFACT_VCS_URL
            version {
                name = ARTIFACT_VERSION
                desc = ARTIFACT_DESCRIPTION
            }
        }
    }
}
