ext {
    dep = [
            androidPlugin: 'com.android.tools.build:gradle:3.3.0',
            jackson: 'com.fasterxml.jackson.core:jackson-core:2.9.8',
            junit: 'junit:junit:4.12',
            fest: 'org.easytesting:fest-assert-core:2.0M10',
    ]
}

subprojects {
    buildscript {
        repositories {
            jcenter()
            google()
        }
    }
    repositories {
        jcenter()
        google()
    }
    configureKotlinLint(it)
}

def configureKotlinLint(Project project) {
    def closure = {
        project.configurations {
            ktlint
        }

        project.dependencies {
            ktlint 'com.github.shyiko:ktlint:0.29.0'
        }

        def ktlintCheck = project.tasks.create('ktlintCheck', JavaExec.class) {
            main = 'com.github.shyiko.ktlint.Main'
            classpath = project.configurations.ktlint
            args 'src/**/*.kt'
        }

        project.check.dependsOn ktlintCheck

        project.tasks.create('ktlintFormat', JavaExec.class) {
            main = 'com.github.shyiko.ktlint.Main'
            classpath = project.configurations.ktlint
            args '-F', 'src/**/*.kt'
        }
    }
    project.plugins.withId('kotlin-android', closure)
    project.plugins.withId('kotlin', closure)
}
